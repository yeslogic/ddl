use codespan::{ByteIndex, FileId, Span};
use codespan_reporting::diagnostic::Diagnostic;
use std::sync::Arc;

use crate::core::{Alias, Extern, Item, Label, Module, Universe, StructType, Term, TypeField};
use crate::diagnostics;
use crate::lexer::Token;
use crate::literal;

grammar(file_id: FileId, report: &mut dyn FnMut(Diagnostic));

extern {
    type Location = ByteIndex;
    type Error = Diagnostic;

    enum Token {
        "doc comment" => Token::DocComment(<String>),
        "inner doc comment" => Token::InnerDocComment(<String>),
        "identifier" => Token::Identifier(<String>),
        "numeric literal" => Token::NumberLiteral(<literal::Number>),
        "string literal" => Token::StringLiteral(<literal::String>),
        "character literal" => Token::CharLiteral(<literal::Char>),

        "bool_elim" => Token::BoolElim,
        "else" => Token::Else,
        "extern" => Token::Extern,
        "f32" => Token::F32,
        "f64" => Token::F64,
        "if" => Token::If,
        "int" => Token::Int,
        "item" => Token::Item,
        "struct" => Token::Struct,

        "{" => Token::OpenBrace,
        "}" => Token::CloseBrace,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,

        "!" => Token::Bang,
        ":" => Token::Colon,
        "," => Token::Comma,
        "=" => Token::Equals,
        ";" => Token::Semi,
    }
}

pub Module: Module = {
    <doc: "inner doc comment"*>
    <items: Item*> => {
        let doc = Arc::from(doc);

        Module { file_id, doc, items }
    },
};

Item: Item = {
    <doc: "doc comment"*>
    <start: @L> "extern" <name: "identifier"> ":" <ty: Term> ";" <end: @R> => {
        let span = Span::new(start, end);
        let doc = Arc::from(doc);
        let name = Label(name);

        Item::Extern(Extern { span, doc, name, ty })
    },
    <doc: "doc comment"*>
    <start: @L> <name: "identifier"> "=" <term: Term> ";" <end: @R> => {
        let span = Span::new(start, end);
        let doc = Arc::from(doc);
        let name = Label(name);

        Item::Alias(Alias { span, doc, name, term })
    },
    <docs: "doc comment"*>
    <start: @L> "struct" <name: "identifier">  "{"
        <mut fields: (<Field> ",")*>
        <last: Field?>
    "}" <end: @R> => {
        let span = Span::new(start, end);
        let doc = Arc::from(docs);
        fields.extend(last);
        let name = Label(name);

        Item::Struct(StructType { span, doc, name, fields })
    },
};

Field: TypeField = {
    <doc: "doc comment"*>
    <start: @L> <name: "identifier"> ":" <term: Term> => {
        let name = Label(name);
        let doc = Arc::from(doc);

        TypeField { doc, start, name, term }
    },
};

Term: Term = {
    TermAtomic,
    <term: TermAtomic> ":" <ty: Term> => Term::Ann(Arc::new(term), Arc::new(ty)),
};

TermAtomic: Term = {
    "(" <term: Term> ")" => term,
    <start: @L> "!" <end: @R> => Term::Error(Span::new(start, end)),
    <start: @L> "item" <name: "identifier"> <end: @R> => Term::Item(Span::new(start, end), Label(name)),
    <start: @L> <name: "identifier"> <end: @R> => {
        let span = Span::new(start, end);
        match name.as_ref() {
            "Type" => Term::Universe(span, Universe::Type),
            "Format" => Term::Universe(span, Universe::Format),
            "Kind" => Term::Universe(span, Universe::Kind),
            _ => {
                report(diagnostics::bug::unknown_global(file_id, &name, span));
                Term::Error(span)
            },
        }
    },
    <start: @L> "bool_elim" <head: Term> "{" <if_true: Term> "," <if_false: Term> "}" <end: @R> => {
        Term::BoolElim(Span::new(start, end), Arc::new(head), Arc::new(if_true), Arc::new(if_false))
    },
    <start: @L> "int" <literal: "numeric literal"> <end: @R> => {
        let span = Span::new(start, end);
        match literal.parse_big_int(file_id, report) {
            Some(value) => Term::IntConst(span, value),
            None => Term::Error(span),
        }
    },
    <start: @L> "f32" <literal: "numeric literal"> <end: @R> => {
        let span = Span::new(start, end);
        match literal.parse_float(file_id, report) {
            Some(value) => Term::F32Const(span, value),
            None => Term::Error(span),
        }
    },
    <start: @L> "f64" <literal: "numeric literal"> <end: @R> => {
        let span = Span::new(start, end);
        match literal.parse_float(file_id, report) {
            Some(value) => Term::F64Const(span, value),
            None => Term::Error(span),
        }
    },
};
